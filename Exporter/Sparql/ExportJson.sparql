prefix ex: <http://id.example.com/schema/>

construct {
    ?asset ex:assetDriId ?assetDriId;
        ex:assetReference ?assetReference;
        ex:assetName ?assetName;
        ex:assetAlternativeName ?assetAlternativeName;
        ex:assetPastReference ?assetPastReference;
        ex:assetDescription ?assetDescription;
        ex:assetSummary ?assetSummary;
        ex:assetTag ?assetTag;
        ex:consignmentTdrId ?consignmentTdrId;
        ex:batchDriId ?batchDriId;
        ex:assetSourceInternalName ?assetSourceInternalName;
        ex:assetConnectedAssetNote ?assetConnectedAssetNote;
        ex:assetRelationDescription ?assetRelationDescription;
        ex:assetPhysicalDescription ?assetPhysicalDescription;
        ex:assetRelationIdentifier ?assetRelationIdentifier;
        ex:paperNumber ?paperNumber;
        ex:assetUsageRestrictionDescription ?assetUsageRestrictionDescription;
        ex:assetHasUkGovernmentWebArchive ?assetHasUkGovernmentWebArchive;
        ex:assetHasLegalStatus ?legalStatus;
        ex:assetHasLanguage ?language;
        ex:assetHasCopyright ?copyright;
        ex:assetHasRetention ?retention;
        ex:assetHasCreation ?creation;
        ex:assetHasAssociatedGeographicalPlace ?associatedGeographicalPlace;
        ex:assetHasOriginDateStart ?originDateStart;
        ex:assetHasOriginDateEnd ?originDateEnd;
        ex:assetHasOriginApproximateDateStart ?originApproximateDateStart;
        ex:assetHasOriginApproximateDateEnd ?originApproximateDateEnd;
        ex:filmProductionCompanyName ?filmProductionCompanyName;
        ex:filmTitle ?filmTitle;
        ex:filmDuration ?filmDuration;
        ex:evidenceProviderName ?evidenceProviderName;
        ex:investigationName ?investigationName;
        ex:inquiryHearingDate ?inquiryHearingDate;
        ex:inquirySessionDescription ?inquirySessionDescription;
        ex:inquiryAssetHasInquiryAppearance ?inquiryAppearance;
        ex:courtSessionDescription ?courtSessionDescription;
        ex:courtSessionDate ?courtSessionDate;
        ex:courtAssetHasCourtCase ?courtCase;
        ex:sealOwnerName ?sealOwnerName;
        ex:sealColour ?sealColour;
        ex:emailAttachmentReference ?emailAttachmentReference;
        ex:sealAssetHasSealCategory ?sealAssetHasSealCategory;
        ex:imageSequenceStart ?imageSequenceStart;
        ex:imageSequenceEnd ?imageSequenceEnd;
        ex:assetHasDimension ?dimension;
        ex:sealAssetHasObverseDimension ?obverseDimension;
        ex:sealAssetHasReverseDimension ?reverseDimension;
        ex:sealAssetHasStartDate ?sealDateStart;
        ex:sealAssetHasEndDate ?sealDateEnd;
        ex:sealAssetHasObverseStartDate ?sealObverseStartDate;
        ex:sealAssetHasObverseEndDate ?sealObverseEndDate;
        ex:sealAssetHasReverseStartDate ?sealReverseStartDate;
        ex:sealAssetHasReverseEndDate ?sealReverseEndDate;
        ex:assetHasChange ?change;
        ex:assetHasSubset ?subset;
        ex:assetHasVariation ?variation;
        ex:assetHasSensitivityReview ?sr;
        ?assetPersonPredicate ?person.
    ?language ex:languageName ?languageName.
    ?copyright ex:copyrightTitle ?copyrightTitle.
    ?creation ex:creationHasFormalBody ?creationHasFormalBody.
    ?creationHasFormalBody ex:formalBodyName ?creationFormalBodyName.
    ?subset ex:subsetHasRetention ?sRetention;
        ex:subsetHasBroaderSubset ?broader;
        ex:subsetHasSensitivityReview ?srs.
    ?sRetention ex:importLocation ?sImportLocation.
    ?srs ex:sensitivityReviewSensitiveName ?sSensitivityReviewSensitiveName.
    ?broader ex:subsetHasNarrowerSubset ?narrower;
        ex:subsetHasRetention ?broaderRetention;
        ex:subsetHasSensitivityReview ?srsb.
    ?broaderRetention ex:importLocation ?sbImportLocation.
    ?srsb ex:sensitivityReviewSensitiveName ?sbSensitivityReviewSensitiveName.
    ?variation ex:variationName ?variationName;
        ex:variationDriManifestationId ?variationDriManifestationId;
        ex:redactedVariationSequence ?redactedVariationSequence;
        ex:variationSequence ?variationSequence;
        ex:variationNote ?variationNote;
        ex:variationRelativeLocation ?variationRelativeLocation;
        ex:variationPhysicalConditionDescription ?variationPhysicalConditionDescription;
        ex:variationReferenceGoogleId ?variationReferenceGoogleId;
        ex:variationReferenceParentGoogleId ?variationReferenceParentGoogleId;
        ex:scannerOperatorIdentifier ?scannerOperatorIdentifier;
        ex:scannerIdentifier ?scannerIdentifier;
        ex:variationHasDatedNote ?datedNote;
        ex:scannedVariationHasScannerGeographicalPlace ?scannerGeographicalPlace;
        ex:scannedVariationHasImageSplit ?scannedVariationHasImageSplit;
        ex:scannedVariationHasImageCrop ?scannedVariationHasImageCrop;
        ex:scannedVariationHasImageDeskew ?scannedVariationHasImageDeskew;
        ex:variationHasSensitivityReview ?srv;
        ex:variationHasChange ?changeV.
    ?datedNote ex:archivistNote ?archivistNote;
        ex:archivistNoteAt ?archivistNoteAt;
        ex:datedNoteHasDate ?noteDate.
    ?noteDate ex:year ?datedNoteYear;
        ex:month ?datedNoteMonth;
        ex:day ?datedNoteDay.
    ?scannerGeographicalPlace ex:geographicalPlaceName ?scannerGeographicalPlaceName.
    ?srv ex:sensitivityReviewHasAccessCondition ?accessCondition;
        ex:sensitivityReviewDate ?vSensitivityReviewDate;
        ex:sensitivityReviewSensitiveName ?vSensitivityReviewSensitiveName;
        ex:sensitivityReviewSensitiveDescription ?vSensitivityReviewSensitiveDescription;
        ex:sensitivityReviewHasSensitivityReviewRestriction ?restriction;
        ex:sensitivityReviewHasChange ?vChange.
    ?accessCondition ex:accessConditionCode ?accessConditionCode;
        ex:accessConditionName ?accessConditionName.
    ?restriction ex:sensitivityReviewRestrictionReviewDate ?sensitivityReviewRestrictionReviewDate;
        ex:sensitivityReviewRestrictionCalculationStartDate ?sensitivityReviewRestrictionCalculationStartDate;
        ex:sensitivityReviewRestrictionDuration ?sensitivityReviewRestrictionDuration;
        ex:sensitivityReviewRestrictionEndYear ?sensitivityReviewRestrictionEndYear;
        ex:sensitivityReviewRestrictionDescription ?sensitivityReviewRestrictionDescription;
        ex:sensitivityReviewRestrictionHasLegislation ?legislation;
        ex:sensitivityReviewRestrictionHasRetentionRestriction ?retentionRestriction.
    ?legislation ex:legislationHasUkLegislation ?legislationHasUkLegislation;
        ex:legislationSectionReference ?legislationSectionReference.
    ?retentionRestriction ex:retentionInstrumentNumber ?retentionInstrumentNumber;
        ex:retentionInstrumentSignatureDate ?retentionInstrumentSignatureDate;
        ex:retentionRestrictionReviewDate ?retentionRestrictionReviewDate;
        ex:retentionRestrictionHasGroundForRetention ?retentionRestrictionHasGroundForRetention.
    ?retentionRestrictionHasGroundForRetention ex:groundForRetentionCode ?groundForRetentionCode;
        ex:groundForRetentionDescription ?groundForRetentionDescription.
    ?retention ex:importLocation ?importLocation;
        ex:retentionHasFormalBody ?retentionHasFormalBody.
    ?retentionHasFormalBody ex:formalBodyName ?retentionFormalBodyName.
    ?vChange ex:changeDriId ?vChangeDriId;
        ex:changeDescription ?vChangeDescription;
        ex:changeDateTime ?vChangeDateTime;
        ex:changeHasOperator ?vChangeOperator.
    ?vChangeOperator ex:operatorIdentifier ?vOperatorIdentifier;
        ex:operatorName ?vOperatorName.
    ?changeV ex:changeDriId ?changeDriIdV;
        ex:changeDescription ?changeDescriptionV;
        ex:changeDateTime ?changeDateTimeV;
        ex:changeHasOperator ?operatorV.
    ?operatorV ex:operatorIdentifier ?operatorIdentifierV;
        ex:operatorName ?operatorNameV.
    ?originDateStart ex:year ?startYear;
        ex:month ?startMonth;
        ex:day ?startDay.
    ?originDateEnd ex:year ?endYear;
        ex:month ?endMonth;
        ex:day ?endDay.
    ?originApproximateDateStart ex:year ?approximateStartYear;
        ex:month ?approximateStartMonth;
        ex:day ?approximateStartDay.
    ?originApproximateDateEnd ex:year ?approximateEndYear;
        ex:month ?approximateEndMonth;
        ex:day ?approximateEndDay.
    ?sealDateStart ex:year ?sealStartYear;
        ex:month ?sealStartMonth;
        ex:day ?sealStartDay.
    ?sealDateEnd ex:year ?sealEndYear;
        ex:month ?sealEndMonth;
        ex:day ?sealEndDay.
    ?sealObverseStartDate ex:year ?sealObverseStartYear;
        ex:month ?sealObverseStartMonth;
        ex:day ?sealObverseStartDay.
    ?sealObverseEndDate ex:year ?sealObverseEndYear;
        ex:month ?sealObverseEndMonth;
        ex:day ?sealObverseEndDay.
    ?sealReverseStartDate ex:year ?sealReverseStartYear;
        ex:month ?sealReverseStartMonth;
        ex:day ?sealReverseStartDay.
    ?sealReverseEndDate ex:year ?sealReverseEndYear;
        ex:month ?sealReverseEndMonth;
        ex:day ?sealReverseEndDay.
    ?dimension ex:firstDimensionMillimetre ?firstDimensionMillimetre;
        ex:secondDimensionMillimetre ?secondDimensionMillimetre.
    ?obverseDimension ex:firstDimensionMillimetre ?obverseFirstDimensionMillimetre;
        ex:secondDimensionMillimetre ?obverseSecondDimensionMillimetre.
    ?reverseDimension ex:firstDimensionMillimetre ?reverseFirstDimensionMillimetre;
        ex:secondDimensionMillimetre ?reverseSecondDimensionMillimetre.
    ?sealAssetHasSealCategory ex:sealCategoryName ?sealCategoryName.
    ?associatedGeographicalPlace ex:geographicalPlaceName ?geographicalPlaceName.
    ?courtCase ex:courtCaseSequence ?courtCaseSequence;
        ex:courtCaseReference ?courtCaseReference;
        ex:courtCaseName ?courtCaseName;
        ex:courtCaseSummary ?courtCaseSummary;
        ex:courtCaseSummaryJudgment ?courtCaseSummaryJudgment;
        ex:courtCaseSummaryReasonsForJudgment ?courtCaseSummaryReasonsForJudgment;
        ex:courtCaseHearingStartDate ?courtCaseHearingStartDate;
        ex:courtCaseHearingEndDate ?courtCaseHearingEndDate.
    ?inquiryAppearance ex:inquiryAppearanceSequence ?inquiryAppearanceSequence;
        ex:inquiryWitnessName ?inquiryWitnessName;
        ex:inquiryWitnessAppearanceDescription ?inquiryWitnessAppearanceDescription.
    ?sr ex:sensitivityReviewSensitiveName ?sensitivityReviewSensitiveName;
        ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescription;
        ex:sensitivityReviewHasChange ?aChange.
    ?aChange ex:changeDriId ?aChangeDriId;
        ex:changeDescription ?aChangeDescription;
        ex:changeDateTime ?aChangeDateTime;
        ex:changeHasOperator ?aChangeOperator.
    ?aChangeOperator ex:operatorIdentifier ?aOperatorIdentifier;
        ex:operatorName ?aOperatorName.
    ?change ex:changeDriId ?changeDriId;
        ex:changeDescription ?changeDescription;
        ex:changeDateTime ?changeDateTime;
        ex:changeHasOperator ?operator.
    ?operator ex:operatorIdentifier ?operatorIdentifier;
        ex:operatorName ?operatorName.
    ?person ex:personGivenName ?personGivenName;
        ex:personFamilyName ?personFamilyName;
        ex:personFullName ?personFullName;
        ex:personHasContactPoint ?personHasContactPoint;
        ex:personHasDateOfBirth ?dateOfBirth;
        ex:personHasBirthAddress ?personHasBirthAddress;
        ex:nationalRegistrationNumber ?nationalRegistrationNumber;
        ex:seamanServiceNumber ?seamanServiceNumber;
        ex:personHasBattalionMembership ?battalionMembership;
        ex:personHasNextOfKinRelationship ?nextOfKinRelationship.
    ?personHasContactPoint ex:geographicalPlaceName ?personHasContactPointPlaceName.
    ?dateOfBirth ex:year ?dateOfBirthYear;
        ex:month ?dateOfBirthMonth;
        ex:day ?dateOfBirthDay.
    ?personHasBirthAddress ex:geographicalPlaceName ?personHasBirthAddressPlaceName.
    ?battalionMembership ex:battalionMembershipHasBattalion ?battalionMembershipHasBattalion.
    ?battalionMembershipHasBattalion ex:battalionName ?battalionName.
    ?nextOfKinRelationship ex:nextOfKinRelationshipHasNextOfKin ?nextOfKinRelationshipHasNextOfKin;
        ex:nextOfKinRelationshipHasKinship ?nextOfKinRelationshipHasKinship.
    ?nextOfKinRelationshipHasNextOfKin ex:personFullName ?nextOfKinPersonFullName.
} where {
    ?asset ex:assetDriId ?assetDriId;
        ex:assetReference ?assetReference;
        ex:assetHasSubset ?subset.
    {
        select ?asset where {
            bind(@id as ?reference)
            {
                ?asset ex:assetReference ?reference.
            }
            union
            {
                ?parentSubset ex:subsetReference ?reference;
                    ex:subsetHasNarrowerSubset*/ex:subsetHasAsset ?asset.
            }
        }
        order by ?asset
        limit @limit offset @offset
    }
    optional { ?asset ex:assetName ?assetName }
    optional { ?asset ex:assetAlternativeName ?assetAlternativeName }
    optional { ?asset ex:assetPastReference ?assetPastReference }
    optional { ?asset ex:assetDescription ?assetDescription }
    optional { ?asset ex:assetSummary ?assetSummary }
    optional { ?asset ex:assetTag ?assetTag }
    optional { ?asset ex:consignmentTdrId ?consignmentTdrId }
    optional { ?asset ex:batchDriId ?batchDriId }
    optional { ?asset ex:assetSourceInternalName ?assetSourceInternalName }
    optional { ?asset ex:assetConnectedAssetNote ?assetConnectedAssetNote }
    optional { ?asset ex:assetRelationDescription ?assetRelationDescription }
    optional { ?asset ex:assetRelationIdentifier ?assetRelationIdentifier }
    optional { ?asset ex:assetPhysicalDescription ?assetPhysicalDescription }
    optional { ?asset ex:paperNumber ?paperNumber }
    optional { ?asset ex:assetUsageRestrictionDescription ?assetUsageRestrictionDescription }
    optional { ?asset ex:assetHasUkGovernmentWebArchive ?assetHasUkGovernmentWebArchive }
    optional { ?asset ex:assetHasLegalStatus ?legalStatus }
    optional {
        ?asset ex:assetHasLanguage ?language.
        ?language ex:languageName ?languageName.
    }
    optional {
        ?asset ex:assetHasCopyright ?copyright.
        ?copyright ex:copyrightTitle ?copyrightTitle.
    }
    optional {
        ?asset ex:assetHasRetention ?retention.
        ?retention ex:importLocation ?importLocation.
        optional {
            ?retention ex:retentionHasFormalBody ?retentionHasFormalBody.
            ?retentionHasFormalBody ex:formalBodyName ?retentionFormalBodyName.
        }
    }
    optional {
        ?asset ex:assetHasCreation ?creation.
        ?creation ex:creationHasFormalBody ?creationHasFormalBody.
        ?creationHasFormalBody ex:formalBodyName ?creationFormalBodyName.
    }
    optional {
        ?asset ex:assetHasAssociatedGeographicalPlace ?associatedGeographicalPlace.
        ?associatedGeographicalPlace ex:geographicalPlaceName ?geographicalPlaceName.
    }
    optional {
        ?asset ex:assetHasOriginDateStart ?originDateStart.
        ?originDateStart ex:year ?startYear.
        optional { ?originDateStart ex:month ?startMonth }
        optional { ?originDateStart ex:day ?startDay }
    }
    optional {
        ?asset ex:assetHasOriginDateEnd ?originDateEnd.
        ?originDateEnd ex:year ?endYear.
        optional { ?originDateEnd ex:month ?endMonth }
        optional { ?originDateEnd ex:day ?endDay }
    }
    optional {
        ?asset ex:assetHasOriginApproximateDateStart ?originApproximateDateStart.
        ?originApproximateDateStart ex:year ?approximateStartYear.
        optional { ?originApproximateDateStart ex:month ?approximateStartMonth }
        optional { ?originApproximateDateStart ex:day ?approximateStartDay }
    }
    optional {
        ?asset ex:assetHasOriginApproximateDateEnd ?originApproximateDateEnd.
        ?originApproximateDateEnd ex:year ?approximateEndYear.
        optional { ?originApproximateDateEnd ex:month ?approximateEndMonth }
        optional { ?originApproximateDateEnd ex:day ?approximateEndDay }
    }
    optional { ?asset ex:filmProductionCompanyName ?filmProductionCompanyName }
    optional { ?asset ex:filmTitle ?filmTitle }
    optional { ?asset ex:filmDuration ?filmDuration }
    optional { ?asset ex:evidenceProviderName ?evidenceProviderName }
    optional { ?asset ex:investigationName ?investigationName }
    optional { ?asset ex:inquiryHearingDate ?inquiryHearingDate }
    optional { ?asset ex:inquirySessionDescription ?inquirySessionDescription }
    optional {
        ?asset ex:inquiryAssetHasInquiryAppearance ?inquiryAppearance
        optional { ?inquiryAppearance ex:inquiryAppearanceSequence ?inquiryAppearanceSequence }
        optional { ?inquiryAppearance ex:inquiryWitnessName ?inquiryWitnessName }
        optional { ?inquiryAppearance ex:inquiryWitnessAppearanceDescription ?inquiryWitnessAppearanceDescription }
    }
    optional { ?asset ex:courtSessionDescription ?courtSessionDescription }
    optional { ?asset ex:courtSessionDate ?courtSessionDate }
    optional {
        ?asset ex:courtAssetHasCourtCase ?courtCase.
        optional { ?courtCase ex:courtCaseSequence ?courtCaseSequence }
        optional { ?courtCase ex:courtCaseReference ?courtCaseReference }
        optional { ?courtCase ex:courtCaseName ?courtCaseName }
        optional { ?courtCase ex:courtCaseSummary ?courtCaseSummary }
        optional { ?courtCase ex:courtCaseSummaryJudgment ?courtCaseSummaryJudgment }
        optional { ?courtCase ex:courtCaseSummaryReasonsForJudgment ?courtCaseSummaryReasonsForJudgment }
        optional { ?courtCase ex:courtCaseHearingStartDate ?courtCaseHearingStartDate }
        optional { ?courtCase ex:courtCaseHearingEndDate ?courtCaseHearingEndDate }
    }
    optional { ?asset ex:sealOwnerName ?sealOwnerName }
    optional { ?asset ex:sealColour ?sealColour }
    optional { ?asset ex:emailAttachmentReference ?emailAttachmentReference }
    optional {
        ?asset ex:sealAssetHasSealCategory ?sealAssetHasSealCategory.
        ?sealAssetHasSealCategory ex:sealCategoryName ?sealCategoryName.
    }
    optional { ?asset ex:imageSequenceStart ?imageSequenceStart }
    optional { ?asset ex:imageSequenceEnd ?imageSequenceEnd }
    optional {
        ?asset ex:assetHasDimension ?dimension.
        optional { ?dimension ex:firstDimensionMillimetre ?firstDimensionMillimetre }
        optional { ?dimension ex:secondDimensionMillimetre ?secondDimensionMillimetre }
    }
    optional {
        ?asset ex:sealAssetHasObverseDimension ?obverseDimension.
        optional { ?obverseDimension ex:firstDimensionMillimetre ?obverseFirstDimensionMillimetre }
        optional { ?obverseDimension ex:secondDimensionMillimetre ?obverseSecondDimensionMillimetre }
    }
    optional {
        ?asset ex:sealAssetHasReverseDimension ?reverseDimension.
        optional { ?reverseDimension ex:firstDimensionMillimetre ?reverseFirstDimensionMillimetre }
        optional { ?reverseDimension ex:secondDimensionMillimetre ?reverseSecondDimensionMillimetre }
    }
    optional {
        ?asset ex:sealAssetHasStartDate ?sealDateStart.
        ?sealDateStart ex:year ?sealStartYear.
        optional { ?sealDateStart ex:month ?sealStartMonth }
        optional { ?sealDateStart ex:day ?sealStartDay }
    }
    optional {
        ?asset ex:sealAssetHasEndDate ?sealDateEnd.
        ?sealDateEnd ex:year ?sealEndYear.
        optional { ?sealDateEnd ex:month ?sealEndMonth }
        optional { ?sealDateEnd ex:day ?sealEndDay }
    }
    optional {
        ?asset ex:sealAssetHasObverseStartDate ?sealObverseStartDate.
        ?sealObverseStartDate ex:year ?sealObverseStartYear.
        optional { ?sealObverseStartDate ex:month ?sealObverseStartMonth }
        optional { ?sealObverseStartDate ex:day ?sealObverseStartDay }
    }
    optional {
        ?asset ex:sealAssetHasObverseEndDate ?sealObverseEndDate.
        ?sealObverseEndDate ex:year ?sealObverseEndYear.
        optional { ?sealObverseEndDate ex:month ?sealObverseEndMonth }
        optional { ?sealObverseEndDate ex:day ?sealObverseEndDay }
    }
    optional {
        ?asset ex:sealAssetHasReverseStartDate ?sealReverseStartDate.
        ?sealReverseStartDate ex:year ?sealReverseStartYear.
        optional { ?sealReverseStartDate ex:month ?sealReverseStartMonth }
        optional { ?sealReverseStartDate ex:day ?sealReverseStartDay }
    }
    optional {
        ?asset ex:sealAssetHasReverseEndDate ?sealReverseEndDate.
        ?sealReverseEndDate ex:year ?sealReverseEndYear.
        optional { ?sealReverseEndDate ex:month ?sealReverseEndMonth }
        optional { ?sealReverseEndDate ex:day ?sealReverseEndDay }
    }
    optional {
        ?asset ex:assetHasChange ?change.
        ?change ex:changeDriId ?changeDriId.
        optional { ?change ex:changeDescription ?changeDescription }
        optional { ?change ex:changeDateTime ?changeDateTime }
        optional {
            ?change ex:changeHasOperator ?operator.
            optional { ?operator ex:operatorIdentifier ?operatorIdentifier }
            optional { ?operator ex:operatorName ?operatorName }
        }
    }
    optional {
        ?asset ex:assetHasSubset ?subset.
        optional {
            ?subset ex:subsetHasRetention ?sRetention.
            ?sRetention ex:importLocation ?sImportLocation.
            optional {
                ?subset ex:subsetHasSensitivityReview ?srs.
                ?srs ex:sensitivityReviewSensitiveName ?sSensitivityReviewSensitiveName.
                filter not exists { ?futureSrs ex:sensitivityReviewHasPastSensitivityReview ?srs }
            }
        }
        optional {
            ?subset ex:subsetHasBroaderSubset+ ?broader.
            ?broader ex:subsetHasNarrowerSubset ?narrower.
            ?narrower ex:subsetHasNarrowerSubset* ?subset.
            optional {
                ?broader ex:subsetHasRetention ?broaderRetention.
                ?broaderRetention ex:importLocation ?sbImportLocation.
                optional {
                    ?broader ex:subsetHasSensitivityReview ?srsb.
                    ?srsb ex:sensitivityReviewSensitiveName ?sbSensitivityReviewSensitiveName.
                    filter not exists { ?futureSrsb ex:sensitivityReviewHasPastSensitivityReview ?srsb }
                }
            }
        }
    }
    optional {
        ?asset ex:assetHasVariation ?variation.
        optional { ?variation ex:variationAlternativeName ?variationAlternativeName }
        optional { ?variation ex:variationName ?varName }
        bind(coalesce(?variationAlternativeName, ?varName) as ?variationName)
        optional { ?variation ex:variationDriManifestationId ?variationDriManifestationId }
        optional { ?variation ex:variationNote ?variationNote }
        optional { ?variation ex:variationRelativeLocation ?variationRelativeLocation }
        optional { ?variation ex:variationPhysicalConditionDescription ?variationPhysicalConditionDescription }
        optional { ?variation ex:variationReferenceGoogleId ?variationReferenceGoogleId }
        optional { ?variation ex:variationReferenceParentGoogleId ?variationReferenceParentGoogleId }
        optional { ?variation ex:scannerOperatorIdentifier ?scannerOperatorIdentifier }
        optional { ?variation ex:scannerIdentifier ?scannerIdentifier }
        optional { ?variation ex:redactedVariationSequence ?redactedVariationSequence }
        optional { ?variation ex:variationSequence ?variationSequence }
        optional {
            ?variation ex:variationHasDatedNote ?datedNote.
            optional { ?datedNote ex:archivistNote ?archivistNote }
            optional { ?datedNote ex:archivistNoteAt ?archivistNoteAt }
            optional {
                ?datedNote ex:datedNoteHasDate ?noteDate.
                ?noteDate ex:year ?datedNoteYear.
                optional { ?noteDate ex:month ?datedNoteMonth }
                optional { ?noteDate ex:day ?datedNoteDay }
            }
        }
        optional {
            ?variation ex:scannedVariationHasScannerGeographicalPlace ?scannerGeographicalPlace.
            ?scannerGeographicalPlace ex:geographicalPlaceName ?scannerGeographicalPlaceName.
        }
        optional { ?variation ex:scannedVariationHasImageSplit ?scannedVariationHasImageSplit }
        optional { ?variation ex:scannedVariationHasImageCrop ?scannedVariationHasImageCrop }
        optional { ?variation ex:scannedVariationHasImageDeskew ?scannedVariationHasImageDeskew }
        optional {
            ?variation ex:variationHasSensitivityReview ?srv.
        	filter not exists { ?futureSr ex:sensitivityReviewHasPastSensitivityReview ?srv }
            optional { 
                ?srv ex:sensitivityReviewHasAccessCondition ?accessCondition.
                ?accessCondition ex:accessConditionCode ?accessConditionCode;
                    ex:accessConditionName ?accessConditionName.
            }
            optional { ?srv ex:sensitivityReviewDate ?vSensitivityReviewDate }
            optional { ?srv ex:sensitivityReviewSensitiveName ?vSensitivityReviewSensitiveName }
            optional { ?srv ex:sensitivityReviewSensitiveDescription ?vSensitivityReviewSensitiveDescription }
            optional { 
                ?srv ex:sensitivityReviewHasSensitivityReviewRestriction ?restriction.
                optional { ?restriction ex:sensitivityReviewRestrictionReviewDate ?sensitivityReviewRestrictionReviewDate }
                optional { ?restriction ex:sensitivityReviewRestrictionCalculationStartDate ?sensitivityReviewRestrictionCalculationStartDate }
                optional { ?restriction ex:sensitivityReviewRestrictionDuration ?sensitivityReviewRestrictionDuration }
                optional { ?restriction ex:sensitivityReviewRestrictionEndYear ?sensitivityReviewRestrictionEndYear }
                optional { ?restriction ex:sensitivityReviewRestrictionDescription ?sensitivityReviewRestrictionDescription }
                optional { 
                    ?restriction ex:sensitivityReviewRestrictionHasLegislation ?legislation.
                    ?legislation ex:legislationHasUkLegislation ?legislationHasUkLegislation.
                    optional { ?legislation ex:legislationSectionReference ?legislationSectionReference }
                }
                optional {
                    ?restriction ex:sensitivityReviewRestrictionHasRetentionRestriction ?retentionRestriction.
                    optional { ?retentionRestriction ex:retentionInstrumentNumber ?retentionInstrumentNumber }
                    optional { ?retentionRestriction ex:retentionInstrumentSignatureDate ?retentionInstrumentSignatureDate }
                    optional { ?retentionRestriction ex:retentionRestrictionReviewDate ?retentionRestrictionReviewDate }
                    optional {
                        ?retentionRestriction ex:retentionRestrictionHasGroundForRetention ?retentionRestrictionHasGroundForRetention.
                        ?retentionRestrictionHasGroundForRetention ex:groundForRetentionCode ?groundForRetentionCode;
                            ex:groundForRetentionDescription ?groundForRetentionDescription.
                    }
                }
            }
            optional {
                ?srv ex:sensitivityReviewHasChange ?vChange.
                ?vChange ex:changeDriId ?vChangeDriId.
                optional { ?vChange ex:changeDescription ?vChangeDescription }
                optional { ?vChange ex:changeDateTime ?vChangeDateTime }
                optional { 
                    ?vChange ex:changeHasOperator ?vChangeOperator.
                    optional { ?vChangeOperator ex:operatorIdentifier ?vOperatorIdentifier }
                    optional { ?vChangeOperator ex:operatorName ?vOperatorName }
                }
            }
        }
        optional {
            ?variation ex:variationHasChange ?changeV.
            ?changeV ex:changeDriId ?changeDriIdV.
            optional { ?changeV ex:changeDescription ?changeDescriptionV }
            optional { ?changeV ex:changeDateTime ?changeDateTimeV }
            optional {
                ?changeV ex:changeHasOperator ?operatorV.
                optional { ?operatorV ex:operatorIdentifier ?operatorIdentifierV }
                optional { ?operatorV ex:operatorName ?operatorNameV }
            }
        }
    }
    optional {
        ?asset ex:assetHasSensitivityReview ?sr.
        filter not exists { ?futureSr ex:sensitivityReviewHasPastSensitivityReview ?sr }
        optional { ?sr ex:sensitivityReviewSensitiveName ?sensitivityReviewSensitiveName }
        optional { ?sr ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescription }
        optional {
            ?sr ex:sensitivityReviewHasChange ?aChange.
            ?aChange ex:changeDriId ?aChangeDriId.
            optional { ?aChange ex:changeDescription ?aChangeDescription }
            optional { ?aChange ex:changeDateTime ?aChangeDateTime }
            optional { 
                ?aChange ex:changeHasOperator ?aChangeOperator.
                optional { ?aChangeOperator ex:operatorIdentifier ?aOperatorIdentifier }
                optional { ?aChangeOperator ex:operatorName ?aOperatorName }
            }
        }
    }
    optional {
        {
            ?asset ex:assetHasPerson ?person.
            bind(ex:assetHasPerson as ?assetPersonPredicate)
        }
        union
        {
            ?asset ex:assetHasVeteran ?person.
            bind(ex:assetHasVeteran as ?assetPersonPredicate)
        }
        optional { ?person ex:personGivenName ?personGivenName }
        optional { ?person ex:personFamilyName ?personFamilyName }
        optional { ?person ex:personFullName ?personFullName }
        optional {
            ?person ex:personHasContactPoint ?personHasContactPoint.
            ?personHasContactPoint ex:geographicalPlaceName ?personHasContactPointPlaceName.
        }
        optional {
            ?person ex:personHasDateOfBirth ?dateOfBirth.
            optional { ?dateOfBirth ex:year ?dateOfBirthYear }
            optional { ?dateOfBirth ex:month ?dateOfBirthMonth }
            optional { ?dateOfBirth ex:day ?dateOfBirthDay }
        }
        optional {
            ?person ex:personHasBirthAddress ?personHasBirthAddress.
            ?personHasBirthAddress ex:geographicalPlaceName ?personHasBirthAddressPlaceName.
        }
        optional { ?person ex:nationalRegistrationNumber ?nationalRegistrationNumber }
        optional { ?person ex:seamanServiceNumber ?seamanServiceNumber }
        optional {
            ?person ex:personHasBattalionMembership ?battalionMembership.
            optional {
                ?battalionMembership ex:battalionMembershipHasBattalion ?battalionMembershipHasBattalion.
                ?battalionMembershipHasBattalion ex:battalionName ?battalionName.
            }
        }
        optional {
            ?person ex:personHasNextOfKinRelationship ?nextOfKinRelationship.
            optional { 
                ?nextOfKinRelationship ex:nextOfKinRelationshipHasNextOfKin ?nextOfKinRelationshipHasNextOfKin.
                optional { ?nextOfKinRelationshipHasNextOfKin ex:personFullName ?nextOfKinPersonFullName }
            }
            optional { ?nextOfKinRelationship ex:nextOfKinRelationshipHasKinship ?nextOfKinRelationshipHasKinship }
        }
    }
}