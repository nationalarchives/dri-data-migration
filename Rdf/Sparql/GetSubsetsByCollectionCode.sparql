prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#>
prefix ex: <http://example.com/schema/>

construct {
    ?subset ex:subsetReference ?reference;
    	ex:subsetHasBroaderSubset [
            ex:subsetReference ?parentReference
    ].
} where {
    {
        select * where {
            ?subset a dri:DeliverableUnit;
                dri:isPartOfSeries/rdfs:label @series;
                rdfs:label ?reference.
            optional { ?subset dri:parent/rdfs:label ?parent }
            optional { ?subset dri:hasDirectory/rdfs:label ?directory }
            bind(coalesce(?parent, ?directory) as ?parentReference)
        }
        order by ?reference
        limit @limit offset @offset
    }
}