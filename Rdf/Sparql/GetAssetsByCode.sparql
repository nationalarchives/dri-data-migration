prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://example.com/schema/>

construct {
    [] ex:assetReference ?reference;
        ex:assetHasSubset [
            ex:subsetReference ?subset
    	];
    	ex:assetHasAssetRetention [
    		ex:custodianshipStartDate ?created
    	].
} where {
    {
        select distinct ?reference ?subset ?created where {
            ?a dri:parent/rdfs:label ?subset;
                dcterms:created ?created;
                dri:parent ?parent.
            ?parent rdfs:label ?reference;
            	dri:isPartOfSeries/rdfs:label @code.
            filter exists { ?a dri:file ?f }
        }
        order by ?reference
        limit @limit offset @offset
    }
}