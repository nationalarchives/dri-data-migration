prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://id.example.com/schema/>

construct {
    [] ex:assetReference ?reference;
        ex:assetHasSubset [
            ex:subsetReference ?subset
    	];
    	ex:assetHasRetention [
            ex:importLocation ?directory
    	].
} where {
    {
        select * where {
            bind(@id as ?code)
            ?a a dri:DeliverableUnit;
    	        rdfs:label ?reference;
    	        dri:isPartOfSeries/rdfs:label ?code.
            optional { ?a dri:parent/dri:hasDirectory/rdfs:label ?directory }
            optional { ?a dri:parent/rdfs:label ?parentLabel }
            filter exists {
                ?m dri:file ?f;
        	        dri:parent ?a.
            }
            bind(coalesce(?parentLabel, ?code) as ?subset)
        }
        order by ?reference
        limit @limit offset @offset
    }
}