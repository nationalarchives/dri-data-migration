prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix prov:  <http://www.w3.org/ns/prov#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://example.com/schema/>

construct {
    ?closure ex:x-type ?type;
            ex:x-reference ?reference;
        ex:sensitivityReviewDriId ?identifier;
        ex:sensitivityReviewDate ?exemptionAsserted;
    	ex:sensitivityReviewScheduledDate ?sensitivityReviewScheduledDate;
    	ex:sensitivityReviewAssetName ?titleAlternative;
    	ex:sensitivityReviewHasPastSensitivityReview [
			ex:sensitivityReviewDriId ?previous
        ];
    	ex:sensitivityReviewHasLegislation ?exemptionCode;
        ex:sensitivityReviewHasSensitivityReviewRestriction [
            ex:sensitivityReviewRestrictionCalculationStartDate ?startDate;
			ex:sensitivityReviewRestrictionDuration ?closurePeriod;
        	ex:sensitivityReviewRestrictionDescription ?reasonForChange
        ];
        ex:sensitivityReviewHasAccessCondition [
            ex:accessConditionCode ?closureType
        ].
}
where {
    {
        select * where {
            bind(@code as ?code)
            ?closure prov:specializationOf ?asset;
                dcterms:identifier ?identifier;
                dri:closureType ?closureType.
            ?asset a ?type;
                rdfs:label ?reference;
            	(dri:isPartOfSeries/rdfs:label)|(dri:directory*/rdfs:label) ?code.
            optional { ?closure dri:closurePeriod ?closurePeriod }
            optional { ?closure dri:startDate ?startDate }
            optional { ?closure dri:exemptionAsserted ?exemptionAsserted }
            optional { ?closure dri:reviewDate ?reviewDate }
            optional { ?closure dri:retentionReconsiderDate ?retentionReconsiderDate }
            optional { ?closure dri:exemptionCode ?exemptionCode }
            optional { ?closure dri:titleAlternative ?titleAlternative }
            optional { ?closure prov:wasRevisionOf/dcterms:identifier ?previous }
            optional {
                ?closure prov:qualifiedInfluence/prov:influencer/rdfs:label ?reasonForChange
            }
            bind(coalesce(?reviewDate, ?retentionReconsiderDate) as ?sensitivityReviewScheduledDate)
        }
        order by ?label
        limit @limit offset @offset
    }
}