prefix ex: <http://id.example.com/schema/>

construct {
    ?asset ex:assetDriId ?assetDriId;
        ex:assetReference ?reference;
        ex:assetHasSubset ?subset;
        ex:assetHasRetention ?retention;
        ex:assetHasTransfer ?assetHasTransfer.
    ?retention ex:importLocation ?importLocation.
    ?assetHasTransfer ex:transferHasFormalBody ?transferHasFormalBody.
} where {
    bind(@id as ?assetDriId)
    ?asset ex:assetDriId ?assetDriId;
        ex:assetReference ?reference.
    optional { ?asset ex:assetHasSubset ?subset }
    optional {
        ?asset ex:assetHasRetention ?retention.
        optional { ?retention ex:importLocation ?importLocation }
    }
    optional {
        ?asset ex:assetHasTransfer ?assetHasTransfer.
        ?assetHasTransfer ex:transferHasFormalBody ?transferHasFormalBody.
    }
}