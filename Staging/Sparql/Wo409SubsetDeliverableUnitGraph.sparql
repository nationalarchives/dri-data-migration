prefix ex: <http://id.example.com/schema/>

construct {
    ?asset ex:assetDriId ?assetDriId;
        ex:wo409SubsetDriXml ?wo409SubsetDriXml;
        ex:wo409SubsetDriId ?wo409SubsetDriId;
        ?assetPersonPredicate ?person.
    ?person ex:personGivenName ?personGivenName;
        ex:personFamilyName ?personFamilyName;
        ex:personFullName ?personFullName;
        ex:personHasContactPoint ?personHasContactPoint;
        ex:personHasDateOfBirth ?dateOfBirth;
        ex:personHasBirthAddress ?personHasBirthAddress;
        ex:personHasBattalionMembership ?battalionMembership;
        ex:nationalRegistrationNumber ?nationalRegistrationNumber;
        ex:personHasNextOfKinRelationship ?nextOfKinRelationship.
    ?dateOfBirth ex:dateVerbatim ?dateOfBirthDateVerbatim;
        ex:year ?dateOfBirthYear;
        ex:month ?dateOfBirthMonth;
        ex:day ?dateOfBirthDay.
    ?battalionMembership ex:battalionMembershipHasBattalion ?battalionMembershipHasBattalion.
    ?nextOfKinRelationship ex:nextOfKinRelationshipHasNextOfKin ?nextOfKinRelationshipHasNextOfKin;
        ex:nextOfKinRelationshipHasKinship ?nextOfKinRelationshipHasKinship.
    ?nextOfKinRelationshipHasNextOfKin ex:personFullName ?nextOfKinPersonFullName.
} where {
    bind(@id as ?assetDriId)
    ?asset ex:assetDriId ?assetDriId.
    optional { ?asset ex:wo409SubsetDriXml ?wo409SubsetDriXml }
    optional { ?asset ex:wo409SubsetDriId ?wo409SubsetDriId }
    optional {
        {
            ?asset ex:assetHasPerson ?person.
            bind(ex:assetHasPerson as ?assetPersonPredicate)
        }
        union
        {
            ?asset ex:assetHasVeteran ?person.
            bind(ex:assetHasVeteran as ?assetPersonPredicate)
        }
        optional { ?person ex:personGivenName ?personGivenName }
        optional { ?person ex:personFamilyName ?personFamilyName }
        optional { ?person ex:personFullName ?personFullName }
        optional { ?person ex:personHasContactPoint ?personHasContactPoint }
        optional {
            ?person ex:personHasDateOfBirth ?dateOfBirth.
            optional { ?dateOfBirth ex:dateVerbatim ?dateOfBirthDateVerbatim }
            optional { ?dateOfBirth ex:year ?dateOfBirthYear }
            optional { ?dateOfBirth ex:month ?dateOfBirthMonth }
            optional { ?dateOfBirth ex:day ?dateOfBirthDay }
        }
        optional { ?person ex:personHasBirthAddress ?personHasBirthAddress }
        optional { ?person ex:nationalRegistrationNumber ?nationalRegistrationNumber }
        optional {
            ?person ex:personHasBattalionMembership ?battalionMembership.
            optional { ?battalionMembership ex:battalionMembershipHasBattalion ?battalionMembershipHasBattalion }
        }
        optional {
            ?person ex:personHasNextOfKinRelationship ?nextOfKinRelationship.
            optional { 
                ?nextOfKinRelationship ex:nextOfKinRelationshipHasNextOfKin ?nextOfKinRelationshipHasNextOfKin.
                optional { ?nextOfKinRelationshipHasNextOfKin ex:personFullName ?nextOfKinPersonFullName }
            }
            optional { ?nextOfKinRelationship ex:nextOfKinRelationshipHasKinship ?nextOfKinRelationshipHasKinship }
        }
    }
}