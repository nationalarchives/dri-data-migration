prefix ex: <http://id.example.com/schema/>

construct {
    ?asset ex:assetDriId ?assetDriId;
        ex:wo409SubsetDriXml ?wo409SubsetDriXml;
        ex:nationalRegistrationNumber ?nationalRegistrationNumber;
        ex:assetHasPerson ?person.
    ?person ex:personGivenName ?personGivenName;
        ex:personFamilyName ?personFamilyName;
        ex:personHasContactPoint ?personHasContactPoint;
        ex:personDateOfBirth ?personDateOfBirth;
        ex:personHasBirthAddress ?personHasBirthAddress;
        ex:personHasBattalionMembership ?battalionMembership;
        ex:personHasNextOfKinRelationship ?nextOfKinRelationship.
    ?battalionMembership ex:battalionMembershipHasBattalion ?battalionMembershipHasBattalion.
    ?nextOfKinRelationship ex:nextOfKinRelationshipHasNextOfKin ?nextOfKinRelationshipHasNextOfKin;
        ex:nextOfKinRelationshipHasKinship ?nextOfKinRelationshipHasKinship.
} where {
    bind(@id as ?assetDriId)
    ?asset ex:assetDriId ?assetDriId.
    optional { ?asset ex:wo409SubsetDriXml ?wo409SubsetDriXml }
    optional { ?asset ex:nationalRegistrationNumber ?nationalRegistrationNumber }
    optional {
        ?asset ex:assetHasPerson ?person.
        optional { ?person ex:personGivenName ?personGivenName }
        optional { ?person ex:personFamilyName ?personFamilyName }
        optional { ?person ex:personHasContactPoint ?personHasContactPoint }
        optional { ?person ex:personDateOfBirth ?personDateOfBirth }
        optional { ?person ex:personHasBirthAddress ?personHasBirthAddress }
        optional {
            ?person ex:personHasBattalionMembership ?battalionMembership.
            optional { ?battalionMembership ex:battalionMembershipHasBattalion ?battalionMembershipHasBattalion }
        }
        optional {
            ?person ex:personHasNextOfKinRelationship ?nextOfKinRelationship.
            optional { ?nextOfKinRelationship ex:nextOfKinRelationshipHasNextOfKin ?nextOfKinRelationshipHasNextOfKin }
            optional { ?nextOfKinRelationship ex:nextOfKinRelationshipHasKinship ?nextOfKinRelationshipHasKinship }
        }
    }
}