prefix ex: <http://id.example.com/schema/>

construct {
    ?sr ex:sensitivityReviewDriId ?sensitivityReviewDriId;
        ex:sensitivityReviewHasSubset ?subset;
        ex:sensitivityReviewHasAsset ?asset;
        ex:sensitivityReviewHasVariation ?variation;
        ex:sensitivityReviewHasAccessCondition ?accessCondition;
        ex:sensitivityReviewDate ?sensitivityReviewDate;
        ex:sensitivityReviewSensitiveName ?sensitivityReviewSensitiveName;
        ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescription;
        ex:sensitivityReviewHasPastSensitivityReview ?pastSensitivityReview;
        ex:sensitivityReviewHasSensitivityReviewRestriction ?restriction;
        ex:sensitivityReviewHasChange ?change.
    ?restriction ex:sensitivityReviewRestrictionReviewDate ?sensitivityReviewRestrictionReviewDate;
        ex:sensitivityReviewRestrictionCalculationStartDate ?sensitivityReviewRestrictionCalculationStartDate;
        ex:sensitivityReviewRestrictionDuration ?sensitivityReviewRestrictionDuration;
        ex:sensitivityReviewRestrictionEndYear ?sensitivityReviewRestrictionEndYear;
        ex:sensitivityReviewRestrictionDescription ?sensitivityReviewRestrictionDescription;
        ex:sensitivityReviewRestrictionHasLegislation ?legislation;
        ex:sensitivityReviewRestrictionHasRetentionRestriction ?retentionRestriction.
    ?retentionRestriction ex:retentionInstrumentNumber ?retentionInstrumentNumber;
        ex:retentionInstrumentSignatureDate ?retentionInstrumentSignatureDate;
        ex:retentionRestrictionReviewDate ?retentionRestrictionReviewDate;
        ex:retentionRestrictionHasGroundForRetention ?retentionRestrictionHasGroundForRetention;
        ex:retentionRestrictionHasRetention ?retention.
    ?asset ex:assetHasRetention ?retention.
    ?subset ex:subsetHasRetention ?retention.
    ?change ex:changeDriId ?changeDriId;
        ex:changeDescription ?changeDescription;
        ex:changeDateTime ?changeDateTime;
        ex:changeHasOperator ?changeOperator.
    ?changeOperator ex:operatorName ?changeAgentLabel.
} where {
    bind(@id as ?sensitivityReviewDriId)
    ?sr ex:sensitivityReviewDriId ?sensitivityReviewDriId.
    optional { ?sr ex:sensitivityReviewHasSubset ?subset }
    optional { ?sr ex:sensitivityReviewHasAsset ?asset }
    optional { ?sr ex:sensitivityReviewHasVariation ?variation }
    optional { ?sr ex:sensitivityReviewHasAccessCondition ?accessCondition }
    optional { ?sr ex:sensitivityReviewDate ?sensitivityReviewDate }
    optional { ?sr ex:sensitivityReviewSensitiveName ?sensitivityReviewSensitiveName }
    optional { ?sr ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescription }
    optional { ?sr ex:sensitivityReviewHasPastSensitivityReview ?pastSensitivityReview }
    optional { 
        ?sr ex:sensitivityReviewHasSensitivityReviewRestriction ?restriction.
        optional { ?restriction ex:sensitivityReviewRestrictionReviewDate ?sensitivityReviewRestrictionReviewDate }
        optional { ?restriction ex:sensitivityReviewRestrictionCalculationStartDate ?sensitivityReviewRestrictionCalculationStartDate }
        optional { ?restriction ex:sensitivityReviewRestrictionDuration ?sensitivityReviewRestrictionDuration }
        optional { ?restriction ex:sensitivityReviewRestrictionEndYear ?sensitivityReviewRestrictionEndYear }
        optional { ?restriction ex:sensitivityReviewRestrictionDescription ?sensitivityReviewRestrictionDescription }
        optional { ?restriction ex:sensitivityReviewRestrictionHasLegislation ?legislation }
        optional {
            ?restriction ex:sensitivityReviewRestrictionHasRetentionRestriction ?retentionRestriction.
            optional { ?retentionRestriction ex:retentionInstrumentNumber ?retentionInstrumentNumber }
            optional { ?retentionRestriction ex:retentionInstrumentSignatureDate ?retentionInstrumentSignatureDate }
            optional { ?retentionRestriction ex:retentionRestrictionReviewDate ?retentionRestrictionReviewDate }
            optional { ?retentionRestriction ex:retentionRestrictionHasGroundForRetention ?retentionRestrictionHasGroundForRetention }
            optional {
                ?retentionRestriction ex:retentionRestrictionHasRetention ?retention.
                optional { ?asset ex:assetHasRetention ?retention }
                optional { ?subset ex:subsetHasRetention ?retention }
            }
        }
    }
    optional {
        ?sr ex:sensitivityReviewHasChange ?change.
        ?change ex:changeDriId ?changeDriId.
        optional { ?change ex:changeDescription ?changeDescription }
        optional { ?change ex:changeDateTime ?changeDateTime }
        optional { 
            ?change ex:changeHasOperator ?changeOperator.
            optional { ?changeOperator ex:operatorName ?changeAgentLabel }
        }
    }
}