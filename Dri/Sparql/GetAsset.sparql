prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://id.example.com/schema/>

construct {
    ?a ex:assetDriId ?a;
        ex:assetReference ?reference;
        ex:assetHasSubset [
            ex:subsetReference ?subset
    	];
        ex:assetHasRetention [
            ex:importLocation ?directory
    	];
        ex:assetHasTransfer ?transferringBody.
} where {
    {
        select distinct ?a ?reference ?subset ?directory ?transferringBody where {
            bind(@id as ?code)
            bind(replace(@id,' ','_') as ?code2)
            ?a rdfs:label ?reference.
            {
                ?a a dri:DeliverableUnit;
                    dri:isPartOfSeries ?series.
                ?series rdfs:label ?code.
            }
            union
            {
                ?a a dri:DeliverableUnit;
                    dri:isPartOfSeries ?series.
                ?series rdfs:label ?code2.
            }
            optional {
                ?a dri:isPartOfUnit ?unit.
                ?batch dri:transferringBody ?transferringBody;
    			    rdfs:label ?batchLabel.
    		    filter(strends(str(?unit), concat('/',?batchLabel)))
            }
            optional { ?a dri:hasDirectory/rdfs:label ?directDirectory }
            optional { ?a dri:parent/dri:hasDirectory/rdfs:label ?parentDirectory }
            optional { ?a dri:parent/rdfs:label ?parentLabel }
            filter exists {
                ?m dri:file ?f;
        	        dri:parent ?a.
            }
            bind(coalesce(?directDirectory, ?parentDirectory) as ?directory)
            bind(coalesce(?parentLabel, ?code) as ?subset)
        }
        order by ?reference
        limit @limit offset @offset
    }
}