prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://id.example.com/schema/>

construct {
    ?d ex:variationDriId ?d;
        ex:variationHasAsset [
	 	    ex:assetReference ?assetReference
    	];
    	ex:variationName ?label.
} where {
    {
        select distinct ?d ?assetReference ?label where {
            bind(@id as ?code)
            bind(replace(@id,' ','_') as ?code2)
            ?d ^dri:file/dri:parent/rdfs:label ?assetReference;
                rdfs:label ?label.
            {
                ?d a dri:File;
        	        dri:directory*/rdfs:label ?code.
            }
            union
            {
                ?d a dri:File;
        	        ^dri:file/dri:parent/dri:isPartOfSeries/rdfs:label ?code.
            }
            union
            {
                ?d a dri:File;
        	        ^dri:file/dri:parent/dri:isPartOfSeries/rdfs:label ?code2.
            }
        }
        order by ?assetReference
        limit @limit offset @offset
    }
}