prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix prov:  <http://www.w3.org/ns/prov#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://id.example.com/schema/>

construct {
    ?closure ex:x-type ?type;
        ex:x-id ?record;
        ex:x-reference ?reference;
        ex:sensitivityReviewDriId ?closure;
        ex:sensitivityReviewDate ?exemptionAsserted;
    	ex:sensitivityReviewSensitiveName ?titleAlternative;
    	ex:sensitivityReviewSensitiveDescription ?descriptionAlternative;
    	ex:sensitivityReviewHasPastSensitivityReview ?previous;
    	ex:sensitivityReviewHasSensitivityReviewRestriction ?srr;
        ex:sensitivityReviewHasAccessCondition ?ac;
        ex:sensitivityReviewHasChange ?qualifiedGeneration.
    ?srr ex:sensitivityReviewRestrictionReviewDate ?reviewDate;
        ex:sensitivityReviewRestrictionCalculationStartDate ?startDate;
		ex:sensitivityReviewRestrictionDuration ?closurePeriod;
        ex:sensitivityReviewRestrictionDescription ?reasonForChange;
        ex:sensitivityReviewRestrictionHasRetentionRestriction ?rr;
        ex:sensitivityReviewRestrictionHasLegislation ?exemptionCode.
    ?rr ex:retentionInstrumentNumber ?rINumber;
        ex:retentionInstrumentSignatureDate ?rISignedDate;
        ex:retentionRestrictionReviewDate ?retentionReconsiderDate;
        ex:retentionRestrictionHasGroundForRetention ?gfr.
    ?gfr ex:groundForRetentionCode ?retentionJustification.
    ?ac ex:accessConditionCode ?closureType.
    ?exemptionCode ex:legislationHasUkLegislation ?exemptionCode.
    ?qualifiedGeneration ex:changeDriId ?qualifiedGeneration;
        ex:changeDescription ?changeLabel;
        ex:changeDateTime ?changeTime;
        ex:changeHasOperator ?changeOperator.
    ?changeOperator ex:operatorIdentifier ?changeOperator;
        ex:operatorName ?changeAgentLabel.
}
where {
    ?closure dri:closureType ?closureType.
    ?record a ?type.
    {
        select ?closure ?record ?reference ?srr ?rr ?gfr ?ac where {
            bind(@id as ?code)
            bind(replace(@id,' ','_') as ?code2)

            #IRIs created to avoid duplication caused by possible multiple ?exemptionCode
            bind(iri(concat(str(?closure),'srr')) as ?srr)
            bind(iri(concat(str(?closure),'rr')) as ?rr)
            bind(iri(concat(str(?closure),'gfr')) as ?gfr)
            bind(iri(concat(str(?closure),'ac')) as ?ac)

            ?closure prov:specializationOf ?record.
            ?record rdfs:label ?reference.
            {
                ?record ^dri:file/dri:parent/dri:isPartOfSeries/rdfs:label ?code.
            }
            union
            {
                ?record ^dri:file/dri:parent/dri:isPartOfSeries/rdfs:label ?code2.
            }
            union
            {
                ?record dri:isPartOfSeries/rdfs:label ?code.
            }
            union
            {
                ?record dri:isPartOfSeries/rdfs:label ?code2.
            }
            filter exists {
                {
                    ?closure dri:titleAlternative ?titleAlternative
                }
                union
                {
                    ?closure dcterms:alternative ?descriptionAlternative
                }
                union
                {
                    ?record a dri:File.
                }
            }
        }
        order by ?reference
        limit @limit offset @offset
    }
    optional { ?closure dri:closurePeriod ?closurePeriod }
    optional { ?closure dri:startDate ?startDate }
    optional { ?closure dri:exemptionAsserted ?exemptionAsserted }
    optional { ?closure dri:reviewDate ?reviewDate }
    optional { ?closure dri:retentionReconsiderDate ?retentionReconsiderDate }
    optional { ?closure dri:exemptionCode ?exemptionCode }
    optional { ?closure dri:titleAlternative ?titleAlternative }
    optional { ?closure dcterms:alternative ?descriptionAlternative }
    optional { ?closure prov:wasRevisionOf ?previous }
    optional {
        ?closure prov:qualifiedInfluence/prov:influencer/rdfs:label ?reasonForChange
    }
    optional { ?closure dri:rINumber ?rINumber }
    optional { ?closure dri:rISignedDate ?rISignedDate }
    optional { ?closure dri:retentionJustification ?retentionJustification }
    optional {
        ?closure prov:qualifiedGeneration ?qualifiedGeneration.
        ?qualifiedGeneration prov:atTime ?changeTime;
    	    prov:activity ?change.
        ?change rdfs:label ?changeLabel;
    	    prov:wasAssociatedWith ?changeOperator.
        ?changeOperator rdfs:label ?changeAgentLabel.
    }
}
