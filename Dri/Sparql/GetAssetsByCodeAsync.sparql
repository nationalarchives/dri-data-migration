prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dri: <http://nationalarchives.gov.uk/terms/dri#> 
prefix dcterms: <http://purl.org/dc/terms/>
prefix ex: <http://id.example.com/schema/>

construct {
    ?a ex:assetDriId ?a;
        ex:assetReference ?reference;
        ex:assetHasSubset [
            ex:subsetReference ?subset
    	];
    	ex:assetHasRetention [
            ex:importLocation ?directory
    	].
} where {
    {
        select distinct ?a ?reference ?subset ?directory where {
            bind(@id as ?code)
            bind(replace(@id,' ','_') as ?code2)
            ?a rdfs:label ?reference.
            {
                ?a a dri:DeliverableUnit;
    	            dri:isPartOfSeries/rdfs:label ?code.
            }
            union
            {
                ?a a dri:DeliverableUnit;
    	            dri:isPartOfSeries/rdfs:label ?code2.
            }
            optional { ?a dri:parent/dri:hasDirectory/rdfs:label ?directory }
            optional { ?a dri:parent/rdfs:label ?parentLabel }
            filter exists {
                ?m dri:file ?f;
        	        dri:parent ?a.
            }
            bind(coalesce(?parentLabel, ?code) as ?subset)
        }
        order by ?reference
        limit @limit offset @offset
    }
}