prefix ex: <http://id.example.com/schema/>

select ?assetDriId ?reference ?redactedVariationSequence
    (group_concat(distinct ?assetName; separator=',') as ?variationName)
	?retentionBodyName ?isPublicDescription 
    ?startOriginDate ?endOriginDate ?accessConditionCode ?closureStatus
    ?sensitivityReviewRestrictionReviewDate
	?sensitivityReviewRestrictionCalculationStartDate
    ?sensitivityReviewRestrictionDuration
	?sensitivityReviewRestrictionEndYear
where {
    bind(@id as ?id)
    ?subset ex:subsetHasBroaderSubset*/ex:subsetReference ?id;
        ex:subsetHasAsset ?asset.
    ?asset ex:assetDriId ?assetDriId;
        ex:assetReference ?reference;
        ex:assetHasVariation ?variation.
    optional { ?asset ex:assetName ?assetName }
    optional {
        ?asset ex:assetHasRetention ?assetHasRetention.
        ?assetHasRetention ex:retentionHasFormalBody ?retentionHasFormalBody.
        ?retentionHasFormalBody ex:formalBodyName ?retentionBodyName.
    }
    optional {
        ?asset ex:variationHasSensitivityReview ?srA.
        filter not exists { ?futureSrA ex:sensitivityReviewHasPastSensitivityReview ?srA }
        optional {
            ?srA ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescriptionA.
        }
    }
    optional {
        ?asset ex:assetHasOriginDateStart ?assetHasOriginDateStart.
        ?assetHasOriginDateStart ex:year ?startYear.
        optional { ?assetHasOriginDateStart ex:month ?startMonth }
        optional { ?assetHasOriginDateStart ex:day ?startDay }
        bind(concat(str(?startYear), coalesce(str(?startMonth), ''), coalesce(str(?startDay), '')) as ?startOriginDate)
    }
    optional {
        ?asset ex:assetHasOriginDateEnd ?assetHasOriginDateEnd.
        ?assetHasOriginDateEnd ex:year ?endYear.
        optional { ?assetHasOriginDateEnd ex:month ?endMonth }
        optional { ?assetHasOriginDateEnd ex:day ?endDay }
        bind(concat(str(?endYear), coalesce(str(?endMonth), ''), coalesce(str(?endDay), '')) as ?endOriginDate)
    }
    optional { ?variation ex:redactedVariationSequence ?redactedVariationSequence }
    optional {
        ?variation ex:variationHasSensitivityReview ?sr.
        filter not exists { ?futureSr ex:sensitivityReviewHasPastSensitivityReview ?sr }
        optional { 
            ?sr ex:sensitivityReviewHasAccessCondition ?ac.
            ?ac ex:accessConditionCode ?accessConditionCode.
            bind(?accessConditionCode as ?closureStatus)
        }
        optional { ?sr ex:sensitivityReviewDate ?sensitivityReviewDate }
        optional {
            ?sr ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescriptionV.
        }
        optional {
            ?sr ex:sensitivityReviewHasSensitivityReviewRestriction ?restriction.
            optional { ?restriction ex:sensitivityReviewRestrictionReviewDate ?sensitivityReviewRestrictionReviewDate }
            optional { ?restriction ex:sensitivityReviewRestrictionCalculationStartDate ?sensitivityReviewRestrictionCalculationStartDate }
            optional { ?restriction ex:sensitivityReviewRestrictionDuration ?sensitivityReviewRestrictionDuration }
            optional { ?restriction ex:sensitivityReviewRestrictionEndYear ?sensitivityReviewRestrictionEndYear }
        }
    }
    bind(coalesce(?sensitivityReviewSensitiveDescriptionV, ?sensitivityReviewSensitiveDescriptionA) as ?isPublicDescription)
}
group by ?assetDriId ?reference ?redactedVariationSequence
    ?retentionBodyName ?isPublicDescription 
    ?startOriginDate ?endOriginDate ?accessConditionCode ?closureStatus
    ?sensitivityReviewRestrictionReviewDate
	?sensitivityReviewRestrictionCalculationStartDate
    ?sensitivityReviewRestrictionDuration
	?sensitivityReviewRestrictionEndYear
order by ?reference ?redactedVariationSequence
limit @limit offset @offset