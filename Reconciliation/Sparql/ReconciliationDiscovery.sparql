prefix ex: <http://id.example.com/schema/>

select ?assetDriId ?reference ?redactedVariationSequence
    ?variationName
	?retentionBodyName ?isPublicDescription
    ?startCoveringDate ?endCoveringDate ?accessConditionCode ?closureStatus
    ?sensitivityReviewRestrictionCalculationStartDate
    ?sensitivityReviewRestrictionDuration
	?sensitivityReviewRestrictionEndYear
where {
    ?asset ex:assetDriId ?assetDriId;
        ex:assetReference ?reference;
        ex:assetHasVariation ?variation.
    {
        select ?asset where {
            bind(@id as ?id)
            ?asset ex:assetHasSubset/ex:subsetHasBroaderSubset*/ex:subsetReference ?id.
        }
        order by ?asset
        limit @limit offset @offset
    }
    optional { ?asset ex:assetName ?assetName }
    optional {
        ?asset ex:assetHasRetention ?assetHasRetention.
        ?assetHasRetention ex:retentionHasFormalBody ?retentionHasFormalBody.
        ?retentionHasFormalBody ex:formalBodyName ?retentionBodyName.
    }
    optional {
        ?asset ex:assetHasSensitivityReview ?srA.
        filter not exists { ?futureSrA ex:sensitivityReviewHasPastSensitivityReview ?srA }
        optional {
            ?srA ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescriptionA.
        }
        optional {
            ?srA ex:sensitivityReviewSensitiveName ?sensitivityReviewSensitiveNameA.
        }
    }
    optional {
        ?asset ex:assetHasOriginDateStart ?assetHasOriginDateStart.
        ?assetHasOriginDateStart ex:year ?startYear.
        optional { ?assetHasOriginDateStart ex:month ?startMonth }
        optional { ?assetHasOriginDateStart ex:day ?startDay }
        bind(concat(str(?startYear), coalesce(str(?startMonth), ''), coalesce(str(?startDay), '')) as ?startOriginDate)
    }
    optional {
        ?asset ex:assetHasOriginDateEnd ?assetHasOriginDateEnd.
        ?assetHasOriginDateEnd ex:year ?endYear.
        optional { ?assetHasOriginDateEnd ex:month ?endMonth }
        optional { ?assetHasOriginDateEnd ex:day ?endDay }
        bind(concat(str(?endYear), coalesce(str(?endMonth), ''), coalesce(str(?endDay), '')) as ?endOriginDate)
    }
    optional { ?asset ex:assetModifiedAt ?assetModifiedAt }
    optional { ?asset ex:assetAlternativeModifiedAt ?assetAlternativeModifiedAt }
    optional {
        ?asset ex:assetHasAlternativeModifiedDateStart ?modifiedDateStart.
        ?modifiedDateStart ex:year ?modifiedStartYear.
        optional { ?modifiedDateStart ex:month ?modifiedStartMonth }
        optional { ?modifiedDateStart ex:day ?modifiedStartDay }
        bind(concat(str(?modifiedStartYear), coalesce(str(?modifiedStartMonth), ''), coalesce(str(?modifiedStartDay), '')) as ?startModifiedDate)
    }
    optional {
        ?asset ex:assetHasAlternativeModifiedDateEnd ?modifiedDateEnd.
        ?modifiedDateEnd ex:year ?modifiedEndYear.
        optional { ?modifiedDateEnd ex:month ?modifiedEndMonth }
        optional { ?modifiedDateEnd ex:day ?modifiedEndDay }
        bind(concat(str(?modifiedEndYear), coalesce(str(?modifiedEndMonth), ''), coalesce(str(?modifiedEndDay), '')) as ?endModifiedDate)
    }
    bind(coalesce(?startOriginDate, str(?assetAlternativeModifiedAt), ?startModifiedDate, str(?assetModifiedAt)) as ?startCoveringDate)
    bind(coalesce(?endOriginDate, str(?assetAlternativeModifiedAt), ?endModifiedDate, str(?assetModifiedAt)) as ?endCoveringDate)
    optional { ?variation ex:redactedVariationSequence ?redactedVariationSequence }
    optional {
        ?variation ex:variationHasSensitivityReview ?sr.
        filter not exists { ?futureSr ex:sensitivityReviewHasPastSensitivityReview ?sr }
        optional { 
            ?sr ex:sensitivityReviewHasAccessCondition ?ac.
            ?ac ex:accessConditionCode ?accessConditionCode.
            bind(?accessConditionCode as ?closureStatus)
        }
        optional { ?sr ex:sensitivityReviewDate ?sensitivityReviewDate }
        optional {
            ?sr ex:sensitivityReviewSensitiveDescription ?sensitivityReviewSensitiveDescriptionV.
        }
        optional {
            ?sr ex:sensitivityReviewSensitiveName ?sensitivityReviewSensitiveNameV.
        }
        optional {
            ?sr ex:sensitivityReviewHasSensitivityReviewRestriction ?restriction.
            optional { ?restriction ex:sensitivityReviewRestrictionCalculationStartDate ?sensitivityReviewRestrictionCalculationStartDate }
            optional { ?restriction ex:sensitivityReviewRestrictionDuration ?sensitivityReviewRestrictionDuration }
            optional { ?restriction ex:sensitivityReviewRestrictionEndYear ?sensitivityReviewRestrictionEndYear }
        }
    }
    bind(coalesce(?sensitivityReviewSensitiveDescriptionV, ?sensitivityReviewSensitiveDescriptionA) as ?isPublicDescription)
    bind(coalesce(?sensitivityReviewSensitiveNameV, ?sensitivityReviewSensitiveNameA, ?assetName) as ?variationName)
}
